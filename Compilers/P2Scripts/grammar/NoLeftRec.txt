# First symbol used is start symbol
# Format is a->b ... For epsilon, simply have empty string be b
prgm -> program id ( id ) ; subprgm_decs comp_stmt . 
prgm -> program id ( id ) ; decs subprgm_decs comp_stmt . 
prgm -> program id ( id ) ; decs comp_stmt . 
prgm -> program id ( id ) ; comp_stmt . 
decs -> var id : type ; decsLR1 
decsLR1 -> var id : type ; decsLR1 
decsLR1 -> 
type -> array [ num .. num ] of std_type 
type -> std_type 
std_type -> real 
std_type -> integer 
subprgm_decs -> subprgm_dec ; subprgm_decsLR1 
subprgm_decsLR1 -> 
subprgm_decsLR1 -> subprgm_dec ; subprgm_decsLR1 
subprgm_dec -> subprgm_head decs subprgm_decs comp_stmt 
subprgm_dec -> subprgm_head subprgm_decs comp_stmt 
subprgm_dec -> subprgm_head decs comp_stmt 
subprgm_dec -> subprgm_head comp_stmt 
subprgm_head -> procedure id ; 
subprgm_head -> procedure id args ; 
args -> ( param_list ) 
param_list -> id : type param_listLR1 
param_listLR1 -> ; id : type param_listLR1 
param_listLR1 -> 
comp_stmt -> begin end 
comp_stmt -> begin opt_stmts end 
opt_stmts -> stmt_list 
stmt_list -> stmt stmt_listLR1 
stmt_listLR1 -> 
stmt_listLR1 -> ; stmt stmt_listLR1 
stmt -> proc_stmt 
stmt -> if expr then stmt else stmt 
stmt -> variable assignop expr 
stmt -> while expr do stmt 
stmt -> comp_stmt 
stmt -> if expr then stmt 
variable -> id [ expr ] 
variable -> id 
proc_stmt -> call id 
proc_stmt -> call id ( expr_list ) 
expr_list -> expr expr_listLR1 
expr_listLR1 -> , expr expr_listLR1 
expr_listLR1 -> 
expr -> simple_expr 
expr -> simple_expr relop simple_expr 
term -> factor termLR1 
termLR1 -> mulop factor termLR1 
termLR1 -> 
simple_expr -> term simple_exprLR1 
simple_expr -> sign term simple_exprLR1 
simple_exprLR1 -> addop term simple_exprLR1 
simple_exprLR1 -> 
factor -> ( expr ) 
factor -> id [ expr ] 
factor -> not factor 
factor -> id 
factor -> num 
sign -> - 
sign -> + 
addop -> - 
addop -> + 
addop -> or 
assignop -> := 
mulop -> * 
mulop -> / 
mulop -> div 
mulop -> mod 
mulop -> and 
relop -> < 
relop -> = 
relop -> <= 
relop -> >= 
relop -> > 
relop -> <> 
