# First symbol used is start symbol
# Format is a->b ... For epsilon, simply have empty string be b
prgm -> program id ( idList ) ; comp_stmt . 
prgm -> program id ( idList ) ; subprgm_decs comp_stmt . 
prgm -> program id ( idList ) ; decs subprgm_decs comp_stmt . 
prgm -> program id ( idList ) ; decs comp_stmt . 
idList -> idList , id 
idList -> id 
decs -> decs var id : type ; 
decs -> var id : type ; 
type -> array [ num .. num ] of std_type 
type -> std_type 
std_type -> integer 
std_type -> real 
subprgm_decs -> subprgm_dec ; 
subprgm_decs -> subprgm_decs subprgm_dec ; 
subprgm_dec -> subprgm_head decs subprgm_decs comp_stmt 
subprgm_dec -> subprgm_head subprgm_decs comp_stmt 
subprgm_dec -> subprgm_head decs comp_stmt 
subprgm_dec -> subprgm_head comp_stmt 
subprgm_head -> procedure id args ; 
subprgm_head -> procedure id ; 
args -> ( param_list ) 
param_list -> id : type 
param_list -> param_list ; id : type 
comp_stmt -> begin end 
comp_stmt -> begin opt_stmts end 
opt_stmts -> stmt_list 
stmt_list -> stmt 
stmt_list -> stmt_list ; stmt 
stmt -> if expr then stmt else stmt 
stmt -> proc_stmt 
stmt -> while expr do stmt 
stmt -> variable assignop expr 
stmt -> if expr then stmt 
stmt -> comp_stmt 
variable -> id [ expr ] 
variable -> id 
proc_stmt -> call id ( expr_list ) 
proc_stmt -> call id 
expr_list -> expr 
expr_list -> expr_list , expr 
expr -> simple_expr 
expr -> simple_expr relop simple_expr 
term -> term mulop factor 
term -> factor 
simple_expr -> simple_expr addop term 
simple_expr -> term 
simple_expr -> sign term 
factor -> id [ expr ] 
factor -> id 
factor -> num 
factor -> ( expr ) 
factor -> not factor 
sign -> + 
sign -> - 
addop -> + 
addop -> - 
addop -> or 
assignop -> := 
mulop -> div 
mulop -> and 
mulop -> mod 
mulop -> / 
mulop -> * 
relop -> >= 
relop -> <> 
relop -> = 
relop -> < 
relop -> > 
relop -> <= 
